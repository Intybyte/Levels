plugins {
    id 'java'
    id 'dev.architectury.loom' version '1.11-SNAPSHOT' // match your version
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow'
}

loom {
    forge {
        mixinConfig "levels.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    minecraft "net.minecraft:minecraft:1.16.5"
    mappings loom.officialMojangMappings()

    forge "net.minecraftforge:forge:$rootProject.forge_version"

    modImplementation "me.shedaniel:architectury-forge:$rootProject.architectury_api_version"

    common(project(path: ':levels-architectury-1.16.5:common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':levels-architectury-1.16.5:common', configuration: 'transformProductionForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
